plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.matthewprenger.cursegradle"
    id "com.modrinth.minotaur"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

version = rootProject.mod_version + "-fabric-" + rootProject.minecraft_version

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"

    modApi "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"

    modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
    inputs.property "version", project.mod_version

    filesMatching("fabric.mod.json") {
        expand "version": project.mod_version
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

modrinth {
    var MODRINTH_TOKEN = System.getenv("MODRINTH_TOKEN")
    if (MODRINTH_TOKEN != null) {
        token = MODRINTH_TOKEN
        projectId = rootProject.modrinth_id
        versionName = project.version
        versionNumber = project.version
        versionType = "release"
        uploadFile = remapJar
        changelog = changelog()
        gameVersions = Arrays.stream(rootProject.fabric_game_versions.split(',')).map(s -> s.trim()).toList()
        loaders = ["fabric"]
        dependencies {
            Arrays.stream(rootProject.modrinth_dependencies.split(',')).forEach(s -> required.project(s))
        }
    }
}

curseforge {
    var CURSEFORGE_TOKEN = System.getenv("CURSEFORGE_TOKEN")
    if (CURSEFORGE_TOKEN != null) {
        apiKey = CURSEFORGE_TOKEN
        project {
            id = rootProject.curseforge_id
            releaseType = "release"
            changelogType = "markdown"
            changelog = changelog()

            addGameVersion "Fabric"
            Arrays.stream(rootProject.fabric_game_versions.split(','))
                .map(s -> s.trim())
                .forEach(s -> addGameVersion(s))

            relations {
                Arrays.stream(rootProject.curseforge_dependencies.split(','))
                    .forEach(s -> requiredDependency(s))
            }
            mainArtifact(remapJar.archivePath) {
                displayName = "AxesAreWeapons-" + project.version
            }
            afterEvaluate {
                uploadTask.dependsOn("build")
            }
        }
        options {
            forgeGradleIntegration = false
            javaVersionAutoDetect = false
        }
    }
}
